plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'   // updated for Gradle 8+
    id 'application'
    id "org.openjfx.javafxplugin" version "0.0.10" apply false
    id 'com.diffplug.spotless' version '6.1.0'
}

/*
 * Uncomment all lines regarding javafx for java 11 support!
 */

group = 'GrizzlyTime'
version = '2.4.0'

def javaVer = getVersion()

application {
    mainClass = "Main"   // replaces deprecated `mainClassName`
}

if (javaVer >= 11) {
    apply plugin: "org.openjfx.javafxplugin"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.json:json:20231013'
    implementation 'com.google.oauth-client:google-oauth-client:1.34.1'
    implementation 'com.google.oauth-client:google-oauth-client-java6:1.34.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.api-client:google-api-client:1.34.1'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20240423-2.0.0'
    implementation 'org.openjfx:javafx-controls:17.0.13'
    implementation 'org.openjfx:javafx-base:17.0.13'
    implementation 'org.openjfx:javafx-graphics:17.0.13'
    implementation 'org.openjfx:javafx-swing:17.0.13'
    implementation 'org.openjfx:javafx-media:17.0.13'
    implementation 'com.google.http-client:google-http-client-gson:1.43.3'
    implementation 'com.google.api-client:google-api-client-gson:1.34.1'




    if (javaVer == 11) {
        implementation "org.openjfx:javafx:11.0.1"
    } else if (javaVer == 17) {
        implementation "org.openjfx:javafx:18-ea+9"
    }
}


if (javaVer >= 11) {
    javafx {
        modules = [
            'javafx.controls',
            'javafx.media',
            'javafx.swing'
        ]
    }
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
            exclude '**/build/**', '**/build-*/**'
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'xml', {
        target fileTree('.') {
            include '**/*.xml'
            exclude '**/build/**', '**/build-*/**'
        }
        eclipseWtp('xml')
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    format 'misc', {
        target fileTree('.') {
            include '**/*.md', '**/.gitignore'
            exclude '**/build/**', '**/build-*/**'
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}

/* https://stackoverflow.com/a/2591122/6755709 */
def getVersion() {
    def version = System.getProperty("java.version");
    if (version.startsWith("1.")) {
        version = version.substring(2, 3);
    } else {
        def dot = version.indexOf(".");
        if (dot != -1) {
            version = version.substring(0, dot);
        }
    }
    return Integer.parseInt(version)
}
